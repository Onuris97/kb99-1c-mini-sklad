
Процедура Печать(ТабДок, Ссылка) Экспорт
	
	Макет = Документы.Реализация.ПолучитьМакет("Печать");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реализация.Дата,
	|	Реализация.Номер,
	|	Реализация.Товары.(
	|		НомерСтроки,
	|		Товар,
	|		Количество,
	|		Цена,
	|		Сумма
	|	)
	|ИЗ
	|	Документ.Реализация КАК Реализация
	|ГДЕ
	|	Реализация.Ссылка В (&Ссылка)";
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьШтрихкод					= Макет.ПолучитьОбласть("Штрихкод");
	ОбластьЗаголовок 				= Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовокПодчеркивание	= Макет.ПолучитьОбласть("ЗаголовокПодчеркивание");
	ОбластьПоставщик 				= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 				= Макет.ПолучитьОбласть("Покупатель");
	ОбластьШапкаТаблицы 			= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрокаТаблицы 			= Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьПодвалТаблицы 			= Макет.ПолучитьОбласть("ПодвалТаблицы");
	ОбластьСуммаПрописью 			= Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьСуммаПодчеркивание		= Макет.ПолучитьОбласть("СуммаПодчеркивание");
	ОбластьПодписи 					= Макет.ПолучитьОбласть("Подписи");
	ТабДок.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		// Вывод заголовка штрихкода
		ТабДок.Вывести(ОбластьШтрихкод);
		
		// Вывод заголовка формы
		ОбластьЗаголовок.Параметры.Заполнить(Выборка);
		ОбластьЗаголовок.Параметры.Дата = Формат(Выборка.Дата, "ДЛФ=DD");
		ТабДок.Вывести(ОбластьЗаголовок);
		
		ТабДок.Вывести(ОбластьЗаголовокПодчеркивание);
		ТабДок.Вывести(ОбластьПоставщик);
		ТабДок.Вывести(ОбластьПокупатель);
		
		// Вывод таблицы
		ТабДок.Вывести(ОбластьШапкаТаблицы);

		ВыборкаТовары = Выборка.Товары.Выбрать();
		СуммаИтог = 0;
		КоличествоИтог = 0;
		Пока ВыборкаТовары.Следующий() Цикл
			ОбластьСтрокаТаблицы.Параметры.Заполнить(ВыборкаТовары);
			ТабДок.Вывести(ОбластьСтрокаТаблицы, ВыборкаТовары.Уровень());
			КоличествоИтог = КоличествоИтог + ВыборкаТовары.Количество;
			СуммаИтог = СуммаИтог + ВыборкаТовары.Сумма;
		КонецЦикла;
		
		ОбластьПодвалТаблицы.Параметры.Всего = СуммаИтог;
		ТабДок.Вывести(ОбластьПодвалТаблицы);
		
		// Вывод итоговой строки и суммы прописью
		СуммаИтогПредставление = Формат(СуммаИтог, "ЧДЦ=2");
		ИтоговаяСтрока = "Всего наименований " + КоличествоИтог + ", на сумму " + СуммаИтогПредставление + " RUB";
		СуммаПрописью = ЧислоПрописью(СуммаИтог, , "рубль, рубля, рублей, м, копейка, копейки, копеек, ж");
		ОбластьСуммаПрописьюПараметры = Новый Структура("ИтоговаяСтрока, СуммаПрописью", ИтоговаяСтрока, СуммаПрописью);
		
		ОбластьСуммаПрописью.Параметры.Заполнить(ОбластьСуммаПрописьюПараметры);
		ТабДок.Вывести(ОбластьСуммаПрописью);
		
		ТабДок.Вывести(ОбластьСуммаПодчеркивание);
		
		// Вывод области подписей
		ОбластьПодписи.Параметры.ОтпускПроизвел = "Иванов И. И.";
		ТабДок.Вывести(ОбластьПодписи);
		
		ВставлятьРазделительСтраниц = Истина;
	КонецЦикла;

КонецПроцедуры
