
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	
	// Очистка старых движений
	Движения.ОстаткиТоваров.Записать();
	Движения.Продажи.Записать();
	
	// Установка блокировки для регистра накопления остатков ОстаткиТоваров
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Товар", "Товар");
	Блокировка.Заблокировать();
	
	// Получение данных
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТовары.Товар КАК Товар,
		|	СУММА(РеализацияТовары.Количество) КАК КоличествоДок,
		|	СУММА(РеализацияТовары.Сумма) КАК СуммаДок
		|ПОМЕСТИТЬ ВТТовары
		|ИЗ
		|	Документ.Реализация.Товары КАК РеализацияТовары
		|ГДЕ
		|	РеализацияТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТовары.Товар
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Товар
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТТовары.Товар КАК Товар,
		|	ВТТовары.Товар.Представление КАК ТоварПредставление,
		|	ВТТовары.КоличествоДок КАК КоличествоДок,
		|	ВТТовары.СуммаДок КАК СуммаДок,
		|	ОстаткиТоваровОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиТоваровОстатки.СебестоимостьОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|	ВТТовары КАК ВТТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваров.Остатки(
		|				&МоментВремени,
		|				Товар В
		|					(ВЫБРАТЬ
		|						ВТТовары.Товар КАК Товар
		|					ИЗ
		|						ВТТовары КАК ВТТовары)) КАК ОстаткиТоваровОстатки
		|		ПО ВТТовары.Товар = ОстаткиТоваровОстатки.Товар
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОстаткиТоваровОстатки.Партия.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(КоличествоДок),
		|	МАКСИМУМ(СуммаДок),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Товар";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаТовар = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаТовар.Следующий() Цикл
		
		// Контроль остатков
		Если ВыборкаТовар.КоличествоДок > ВыборкаТовар.КоличествоОстаток Тогда
			Нехватка = ВыборкаТовар.КоличествоДок - ВыборкаТовар.КоличествоОстаток;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает товара """ + ВыборкаТовар.ТоварПредставление + """ в количестве " + Нехватка + " шт.";
			Сообщение.Сообщить();
			
			Отказ = Истина;
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		// Получить количество списания по текущему товару
		КоличествоСписания = ВыборкаТовар.КоличествоДок;
		
		ИтогоСписаннаяСебестоимость = 0;
		
		// Цикл по партиям
		ВыборкаПартия = ВыборкаТовар.Выбрать();
		Пока ВыборкаПартия.Следующий() И КоличествоСписания <> 0 Цикл
			
			// Получить количество списания по текущей партии
			КоличествоТекущегоСписания = Мин(КоличествоСписания, ВыборкаПартия.КоличествоОстаток);
			
			// Расчёт списываемой себестоимости
			Если КоличествоТекущегоСписания = ВыборкаПартия.КоличествоОстаток Тогда
				Себестоимость = ВыборкаПартия.СуммаОстаток;
			Иначе
				Себестоимость = КоличествоТекущегоСписания / ВыборкаПартия.КоличествоОстаток * ВыборкаПартия.СуммаОстаток;
			КонецЕсли;
			
			// Движение по регистру ОстаткиТоваров
			Движение = Движения.ОстаткиТоваров.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Товар = ВыборкаПартия.Товар;
			Движение.Партия = ВыборкаПартия.Партия;
			Движение.Количество = КоличествоТекущегоСписания;
			Движение.Себестоимость = Себестоимость;
			
			// Добавляем списанную себестоимость в итог
			ИтогоСписаннаяСебестоимость = ИтогоСписаннаяСебестоимость + Себестоимость;
			
			// Вычитаем списанное количество
			КоличествоСписания = КоличествоСписания - КоличествоТекущегоСписания;

		КонецЦикла;
		
		// Движение по регистру Продажи
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Товар = ВыборкаТовар.Товар;
		Движение.Количество = ВыборкаТовар.КоличествоДок;
		Движение.Себестоимость = ИтогоСписаннаяСебестоимость;
		Движение.Продажа = ВыборкаТовар.СуммаДок;
		
	КонецЦикла;
	
КонецПроцедуры
